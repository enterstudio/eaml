<S> = res-def+ (* A program is composed of 1 or more resource definitions *)

(* there are currently only 3 types of resources *)
<res-def> = simple-res-def
          | style-def

(* simple resources *)
simple-res-def     = simple-res-type identifier <':'> simple-res-single-config <';'>
                   | simple-res-type identifier <'{'> simple-res-configs <'}'>

simple-res-single-config = simple-res-value
simple-res-configs       = ( simple-res-config )*
simple-res-config        = config-name <':'> simple-res-value <';'>
<simple-res-type>        = 'color' | 'dimen' | 'bool' | 'integer' | 'string'
<simple-res-value>       = string-literal
                         | pointer
                         | color-literal
                         | dimen-literal
                         | native-pointer
                         | boolean-literal
                         | integer-literal

(* styles *)
style-def     = <'style'> identifier (<'<'> identifier)? <'{'> attrs <'}'>
config-def    = config-name (<','> config-name)* <'{'> attrs <'}'>

attrs         = (attr-def | config-def)*
attr-def      = attr-name <':'> attr-value <';'>
<attr-value>  = string-literal
              | color-literal
              | dimen-literal
              | native-pointer
              | pointer

(* tokens *)
<identifier>    = #"[a-zA-Z]\w+"
pointer         = #"[a-zA-Z]\w+"
boolean-literal = 'true' | 'false'
string-literal  = #'".*?"' | #"'.*?'"
integer-literal = #"\d+"
color-literal   = #'#[a-fA-F0-9]{3}([a-fA-F0-9]{3})?'
dimen-literal   = #'\d+(sp|dp|px|dip)'
config-name     = #"[a-z][a-z0-9]*(-[a-z0-9]+)*"
<attr-name>     = #"[a-z]+(:\w+)*"
native-pointer  = #"@\w+/\w+"
